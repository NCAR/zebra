#!/bin/tcsh
#
# Run another display session
#
echo $0 started at `date`

if (! $?ZEBRA_MASTER || ! $?ZEBRA_PROJDIR) then
    echo "ZEBRA_MASTER and ZEBRA_PROJDIR must be set for newdisplay"
    exit 1
endif

#
# Set up for a remote DS_Daemon if we are not on ZEBRA_MASTER
#
set dshost=""
if ($ZEBRA_MASTER != `hostname`) then
    if ($ZEBRA_MASTER != `hostname`) setenv DS_DAEMON_HOST $ZEBRA_MASTER
    set dshost = "-dshost $DS_DAEMON_HOST"
endif

#
# Run an event logger and show status of last data received
#
EventLogger -w &
LastData -g 240x200-0+175 $ZEBRA_PROJDIR/LastData.conf &

#
# Run a zebra display and wait for it to terminate
#
xterm -name Zebra -title Zebra -iconic -e \
    zstart $dshost -dm -preserve $ZEBRA_PROJDIR

# 
# Get rid of the LastData and EventLogger, then quit
#
kill %1
kill %2
sleep 1

echo $0 finished at `date`

#
# If we were started by a startbutton, fire it up again
#
set execdir = $0:h	# look in the same dir that we were grabbed from
if ($?STARTBUTTON_USED) $execdir/startbutton &
