#!/bin/tcsh
#
# Real-time Zebra startup for S-Pol
#
echo $0 started at `date`
set execdir = $0:h

#
# Make sure ZEBRA_MASTER and ZEBRA_PROJDIR are set
#
if (! $?ZEBRA_MASTER || ! $?ZEBRA_PROJDIR) then
    echo "${0}: ZEBRA_MASTER and ZEBRA_PROJDIR must be set!"
    exit 1
endif

#
# Subset the RHIs presented to Zebra in real-time 
# (this environment variable is tested in ds.config)
#
setenv ZEBRA_SUBSET_RHI

#
# Make a good guess for XAPPLRESDIR
#
set zstart = `which zstart`
set libdir = $zstart:h/../lib	# should end up as <zebra_installdir>/lib
setenv XAPPLRESDIR $libdir/resources

#
# Fire up Zebra with the datastore running on ZEBRA_MASTER 
# and the display on the local machine 
#
zstart -n -dshost $ZEBRA_MASTER -dsonly $ZEBRA_PROJDIR
if ($status != 0) exit $status
$execdir/newdisplay &

#
# Run some extra stuff if we're on the master machine and an
# instance of Archiver is not running
#
if ($?SKIP_EXTRAS) exit 0

mstatus | fgrep Archiver >& /dev/null
@ need_extras = $status

if ( `hostname` == $ZEBRA_MASTER && $need_extras ) then

    set logdir = /ops/run

    set archiverlog = $logdir/arch.out
    rm -f $archiverlog	# AAAAARRRRGH!
    /zebra/bin/Archiver -f /dev/st0 -g +5-85 -time 60 \
		    -database /ops/run/ArcDB \
		    -tapelimit 10000000 \
		    -exclude spol >& $archiverlog  &
    sleep 2

    #
    # Start the rain, hydrometeor ID, and refractivity stuff
    #
    set rainlog = $logdir/rain.out
    set hyidlog = $logdir/hyid.out
    set refractlog = $logdir/refract.out

    /bin/mv -f $rainlog $rainlog.save > /dev/null
    startboth >& $rainlog
    /bin/mv -f $hyidlog $hyidlog.save > /dev/null
    starthyid < /dev/null >& $hyidlog &
#    ssh -n -x ap.spol.rtf start_refract >& $refractlog &
    ssh -n -x ap.spol.rtf start_refract 

    # 
    # Start up building of selected sweepfile links
    #
    killall build_selected_links
    setenv PYTHONUNBUFFERED 1
    set bslog=/tmp/build_select.log
    rm -f $bslog
    /zebra/bin/build_selected_links >& $bslog &

    #
    # Watch for GOES files from the CIDD machine
    #
    set goes_watch_log = /tmp/goes_watch.log
    rm -f $goes_watch_log
    $ZEBRA_PROJDIR/goes_watch >& $goes_watch_log &

    # startup Campbell weather station ingest
    start_cam &

endif
