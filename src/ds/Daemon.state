!
! $Id: Daemon.state,v 2.1 1991-09-24 21:23:35 martin Exp $
!
! The data store daemon configuration language.
!
define state initial
	eoftext 'exit'
	ctable ui$command_table
	@ /rdss/ui/ui_commands.

	input 'platform' 1
		next ust$final_unmunged_string
	endinput
	input 'subplatform' 12
		next subplatform-parent
	endinput
	input 'done' 8
		next ust$no_more
	endinput
	input 'truncate' 14
		next trunc-how-much
	endinput
	input 'every' 15
		next every-interval
	endinput
	input 'broadcast' 17
		next bcast-address
	endinput
	input 'receive' 18
		next ust$final_int
	endinput
enddef



define state bcast-address
	input string
		next ust$final_int
	endinput
enddef


!
! Stuff for platform definition
!
define state in-platform
	input 'endplatform' 2
		next ust$no_more
	endinput
	input 'organization' 3
		next organization
	endinput
	input 'filetype' 4
		next file-type
	endinput
	input 'keep' 5
		next ust$final_string
	endinput
	input 'regular' 6
		next ust$final_int
	endinput
	input 'mobile' 7
		next ust$no_more
	endinput
	input 'composite' 9
		next ust$no_more
	endinput
	input 'discrete' 10
		next ust$no_more
	endinput
	input 'directory' 11
		next ust$final_unmunged_string
	endinput
	input 'maxsamples' 13
		next ust$final_int
	endinput
	input 'remote' 16
		next ust$final_unmunged_string
	endinput
enddef



!
! File types -- must match those in dsPrivate.h
!
define state file-type
	input 'netcdf' 0
		next ust$no_more
	endinput
	input 'boundary' 1
		next ust$no_more
	endinput
	input 'raster' 2
		next ust$no_more
	endinput
	input 'compressed_raster' 3
		next ust$no_more
	endinput
enddef





!
! Possible organizations -- must match those in DataStore.h
!
define state organization
	input 'image' 4
		next ust$no_more
	endinput
	input 'grid' 1
		next ust$no_more
	endinput
	input '1dgrid' 8
		next ust$no_more
	endinput
	input '2dgrid' 1
		next ust$no_more
	endinput
	input '3dgrid' 6
		next ust$no_more
	endinput
	input 'irgrid' 2
		next ust$no_more
	endinput
	input 'scalar' 3
		next ust$no_more
	endinput
	input 'outline' 5
		next ust$no_more
	endinput
enddef


!
! Definition of subplatforms.
!

define state subplatform-parent
	input string
		next subplatform-list
	endinput
enddef

define state subplatform-list
	input string
		next continue-subplatform
	endinput
enddef

define state continue-subplatform
	input string
		next continue-subplatform
	endinput
	input EOS
		done
	endinput
enddef


define state trunc-how-much
	input integer
		next trunc-which
	endinput
	input 'default' 0
		next trunc-which
	endinput
enddef

define state trunc-which
	input EOS
		done
	endinput
	input 'all' 0
		next ust$no_more
	endinput
	input string
		lowercase
		next ust$no_more
	endinput
enddef


define state every-interval
	input integer
		next ust$final_unmunged_string
	endinput
enddef



set ui$initial_state "initial"

procedure ui$init
	if (defined ("initfile"))
		local rcmd concat("read ", initfile)
	else
		local rcmd 'read /fcc/DataStore/ds.config'
	endif
	#rcmd
	done
endprocedure


