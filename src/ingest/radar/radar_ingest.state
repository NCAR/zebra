!
! Radar ingest configuration language.
!
define state initial
	eoftext 'exit'
	ctable ui$command_table
	@ /rdss/ui/ui_commands.

	input 'go' 1
		next ust$no_more
	endinput
	input 'source' 2
		next source-type
	endinput
	input 'field' 5
		next field-name
	endinput
	input 'threshold' 6
		next threshold-offset
	endinput
	input 'consumer' 7
		next consumer-args
	endinput
enddef


define state field-name
	input string
		lowercase
		next field-opt-offset
	endinput
enddef

define state field-opt-offset
	input integer
		next ust$no_more
	endinput
	input 'offset' 0
		ignore
		next field-opt-offset
	endinput
enddef

define state source-type
	input 'file' 3
		next ust$final_unmunged_string
	endinput
	input 'interface' 4
		next ust$final_unmunged_string
	endinput
enddef


define state threshold-offset
	input 'offset' 0
		ignore
		next threshold-offset
	endinput
	input integer
		next threshold-counts
	endinput
enddef

define state threshold-counts
	input 'threshold' 0
		ignore
		next threshold-counts
	endinput
	input integer
		next ust$no_more
	endinput
enddef


define state consumer-args
	input EOS
		done
	endinput
	input string
		next consumer-args
	endinput
enddef

set ui$initial_state "initial"

procedure ui$init
!
! Set up for CP4 at Marshall.
!
	set x_resolution	800
	set y_resolution	800
	set radar_lat		39.95
	set radar_lon		-105.19
	set radar_alt		1.750		! Not used now
	set GMT_offset		7
	set platform		"cp4"
	set minimum_sweep	50
!
! If there was a command file specified, we just read it and go
! straight into run mode.  Otherwise we expect to be interactive.
!
	if (defined ("CommandFile"))
		local rcmd "read"
		message 'Reading command file'
		#rcmd #CommandFile
		go
	else
		message 'No command file'
	endif
endprocedure
