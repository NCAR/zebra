C   THIS IS SSEC PROPRIETARY SOFTWARE - ITS USE IS RESTRICTED.
      FUNCTION JULDAY(JYEAR,MONTH,JDAY)
C *** McIDAS Revision History ***
C 1 JULDAY.FOR 17-Apr-90,8:02:16,`SSEC' PC-McIDAS ver 5.00
C 2 JULDAY.FOR 24-Sep-90,18:19:46,`SMG' First Release into COMmon
C 3 JULDAY.FOR 1-Apr-94,1:19:18,`BARRYR' Add proprietary statement
C 4 JULDAY.FOR 2-May-94,16:51:28,`USER' Released
C *** McIDAS Revision History ***
C     VERSION OF 28 SEP 79
C     TO GET THE JULIAN DAY NUMBER OF THE DAY BEGINNING
C     AT 12 Z OF THE GIVEN CALENDAR DATE.  IF JYEAR IS GIVEN WITH
C     ONLY TWO DIGITS, THE 20TH CENTURY IS ASSUMED.  THE ROUTINE IS
C     VALID FOR ANY DATE OF THE GREGORIAN CALENDAR LATER THAN
C     1 JAN 100 AD, AND EARLIER THAN 1 JAN 4000 AD, SINCE
C     YEARS DIVISIBLE BY 4000 ARE COMMON YEARS IN THE GREGORIAN
C     CALENDAR.  IF MONTH = 0, THEN JDAY IS THE NUMERIC DAY OF THE YEAR.
C
      IF(JYEAR.LT.0 .OR. MONTH.LT.0 .OR. MONTH.GT.12) GO TO 160
      IF(JDAY.LE.0 .OR. JDAY.GT.366) GO TO 160
      IF(MONTH.NE.0 .AND. JDAY.GT.31) GO TO 160
C
      JYR = JYEAR
      IF(JYR .LT. 100) JYR = JYR + 1900
      KXTRA = 1
      KWADS = JYR/400
      JYRUP = 400*KWADS
      NQUADS = (JYR - JYRUP)/4
      JULIAN = 1721060 + 146097*KWADS + 1461*NQUADS - NQUADS/25
C      1721060 IS THE J.D.N. OF 1 JAN 00 AD.
      IF(MOD(NQUADS,25).NE.0 .OR. NQUADS.EQ.0) GO TO 110
C     IN A 'SHORT' QUADRENNIUM
      JULIAN = JULIAN + 1
      KXTRA = 0
C     'JULIAN' NOW POINTS TO THE FIRST DAY OF THE QUADRENNIUM CON-
C     TAINING THE GIVEN DATE.
C
  110 JYRUP = JYRUP + 4*NQUADS
      NYEARS = JYR - JYRUP
      IF(NYEARS .GE. 1) GO TO 150
      IF(MOD(JYR,100).EQ.0 .AND. MOD(JYR,400).NE.0) GO TO 150
C
C     LEAP YEAR
      KODE = 1115208
C
  120 IF(MONTH .LE. 1) GO TO 140
      DO 130 J = 2,MONTH
      JULIAN = JULIAN + 31 - MOD(KODE,4)
  130 KODE = KODE/4
C
  140 JULDAY = JULIAN + JDAY - 1
      RETURN
C
C     COMMON YEAR
  150 KODE = 1115212
      JULIAN = JULIAN + 365*NYEARS + KXTRA
      GO TO 120
C
  160 WRITE(6,170) JYEAR,MONTH,JDAY
  170 FORMAT(' ERROR IN ARGUMENTS TO JULDAY ',3I12)
      JULDAY = 0
      RETURN
      END
