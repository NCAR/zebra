      SUBROUTINE DECODE(I85GHZ,ITB,IADJ,IREC,LREC)
C
C $Id: decode.f,v 1.1 1993-05-21 23:51:19 granger Exp $
C 
C ****************** DESCRIPTION OF ARGUMENTS **************************
C     I85GHZ=0 PROCESSES ONLY THE 19, 22, AND 37 GHZ OBS.
C     I85GHZ=1 PROCESSES ALL CHANNELS
C
C     ITB=0 COMPUTES TA'S
C     ITB=1 COMPUTES TB'S
C
C     IADJ=0 DOES NO ALONGTRACK LOCATION CORRECTION
C     IADJ=1 DOES AN ALONGTRACK LOCATION CORRECTION
C
C     IREC DENOTES LOGICAL RECORD NUMBER (1 THROUGH 16)
C
C     LREC IS THE ARRAY THAT CONTAINS THE BYTES IN THE TAPE BLOCK
C     THE USER MUST SUPPLY THE TAPE READ ROUTINE FOR FILLING LREC
C     BEFORE CALLING THIS ROUTINE 
C
C ************ DECLARATION OF VARIABLES, ARRAYS, AND COMMONS ***********
C
      CHARACTER*1 LREC(1784,16)
C
      INTEGER*2 IBUF
      COMMON /INDATA/ IBUF(1784)
C
      REAL*8 REV,XTIME
      INTEGER*4 ITIME,ITIMSC,IVOLT,IAGC,ICOLDA,IHOTA,ICOLDB,IHOTB,IASCTM
      INTEGER*4 IATOIL,IBTOIL,ISAT
      REAL*4 XLATSC,XLONSC,ALTSC,THT,HLTEMP,RFTEMP,FRTEMP
      REAL*4 PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,SPACER
      REAL*4 ALAT,ALON,BLAT,BLON,TALO,ATAHI,BTAHI
      COMMON/OUTDAT/ REV,XTIME,ITIME,ITIMSC,XLATSC,XLONSC,ALTSC,THT,
     1 HLTEMP(3),IVOLT(2),RFTEMP,FRTEMP,IAGC(6),
     2 IASCTM,PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,ISAT,SPACER(6),
     2 ICOLDA(5,7),IHOTA(5,7),ICOLDB(5,2),IHOTB(5,2),
     3 ALAT(128),ALON(128),BLAT(128),BLON(128),
     4 TALO(5,64),ATAHI(2,128),BTAHI(2,128),IATOIL(128),IBTOIL(128)
C
C     ********************** DATA INITIALIZATION ***********************
C
      DATA N1,N2,N3/256,65536,16777216/
      DATA RAD/0.017453293/
C
C ************************ BEGIN EXECUTION ****************************
C
C     TRANSFER TAPE BYTES IN IREC LOGICAL RECORD TO ARRAY IBUF
C
      DO 100 I=1,1784
      IBUF(I)=ICHAR(LREC(I,IREC))
  100 CONTINUE
C
C     FIND SCAN TIME
C
      ITIME= N3*IBUF( 1)+N2*IBUF( 2)+N1*IBUF( 3)+IBUF( 4)
      IFRCTM=N3*IBUF(17)+N2*IBUF(18)+N1*IBUF(19)+IBUF(20)
      IF(IFRCTM.EQ.0) XTIME=ITIME
      IF(IFRCTM.NE.0) XTIME=ITIME+1.D-4*(IFRCTM-10000)
C
C     FIND SPACECRAFT TIME AND ORBIT NUMBER
C     IOLD=1 FOR ALL SCANS BEFORE 1989
C
      IF(ITIME.LT.63163966) THEN
      IOLD=1
      ITIMSC=    N3*IBUF( 9)+N2*IBUF(10)+N1*IBUF(11)+IBUF(12)
      ELSE
      IOLD=0
      ITIMSC=ITIME
      ENDIF
C
      IF(ITIME.GE.63163966.AND.ITIME.LT.84156110) THEN
      REV=1.D-4*(N3*IBUF( 9)+N2*IBUF(10)+N1*IBUF(11)+IBUF(12))
      ELSE
      REV=1.D-4*(N3*IBUF( 5)+N2*IBUF( 6)+N1*IBUF( 7)+IBUF( 8))
      ENDIF
C 
C     FIND SPACECRAFT LATITUDE, LONGITUDE, AND ALTITUDE
C
      XLATSC=1.D-6*(N3*IBUF(13)+N2*IBUF(14)+N1*IBUF(15)+IBUF(16))-90.
      XLONSC=1.D-6*(N3*IBUF(21)+N2*IBUF(22)+N1*IBUF(23)+IBUF(24))
      ALTSC= 1.D-3*(N3*IBUF(25)+N2*IBUF(26)+N1*IBUF(27)+IBUF(28))
C
C     DETERMINE SATELLITE NUMBER AND INCIDENCE ANGLE
C     FACTOR OF .7040147 = SIN(44.75 DEG)
C     FACTOR OF .7116583 = SIN(45.37 DEG)
C
      IF(ITIME.GT.144554200) THEN
      ITERM=N3*IBUF( 9)+N2*IBUF(10)+N1*IBUF(11)+IBUF(12)
      THT=0.001*INT(ITERM/1000)
      ISAT=ITERM-1000*INT(ITERM/1000)
C
      ELSE
C     EARTHR=RADIUS OF CURVATURE OF EARTH'S SURFACE
      EARTHR=6345.7+55.0*SIN(RAD*XLATSC)**2
      DIFREV=DABS(REV-300-(XTIME-16530609)/6118.)
      IF(DIFREV.LT.100.) THEN
      ISAT=8
      THT=ASIN(0.7040147*(EARTHR+ALTSC)/EARTHR)/RAD
      ELSE
      ISAT=10
      THT=ASIN(0.7116583*(EARTHR+ALTSC)/EARTHR)/RAD
      ENDIF
C
      ENDIF
C
C     FIND RADIOMETER CALIBRATION DATA
C
      HLTEMP(3)=0.01*(N1*IBUF(29)+IBUF(30))
      HLTEMP(2)=0.01*(N1*IBUF(31)+IBUF(32))
      HLTEMP(1)=0.01*(N1*IBUF(33)+IBUF(34))
      RFTEMP=   0.01*(N1*IBUF(39)+IBUF(40))
      FRTEMP=   0.01*(N1*IBUF(41)+IBUF(42))
      IVOLT(2)=       N1*IBUF(35)+IBUF(36)
      IVOLT(1)=       N1*IBUF(37)+IBUF(38)
      IAGC(3)=        N1*IBUF(43)+IBUF(44)
      IAGC(2)=        N1*IBUF(45)+IBUF(46)
      IAGC(1)=        N1*IBUF(47)+IBUF(48)
C
      N=-2
      DO 200 ICH=1,7
      IF(ICH.NE.7) SPACER(ICH)=0.
      DO 200 IP=1,5
      N=N+2
      ICOLDA(IP,ICH)=N1*IBUF(N + 77)+IBUF(N + 78)
      IHOTA(IP,ICH)= N1*IBUF(N +147)+IBUF(N +148)
  200 CONTINUE
C
      IAGC(6)=N1*IBUF(217)+IBUF(218)
      IAGC(5)=N1*IBUF(219)+IBUF(220)
      IAGC(4)=N1*IBUF(221)+IBUF(222)
C
      N=-2
      DO 300 ICH=1,2
      DO 300 IP=1,5
      N=N+2
      ICOLDB(IP,ICH)=N1*IBUF(N +223)+IBUF(N +224)
      IHOTB(IP,ICH)= N1*IBUF(N +243)+IBUF(N +244)
  300 CONTINUE
C
C     FIND ORBIT PARAMETERS
C
      IF(ITIME.LT.84156110) THEN
      IASCTM=0
      PERIOD=0.
      ASCLOC=0.
      ANGINC=0.
      AXIS=0.
      ECC=0.
      ANGPER=0.
C
      ELSE
      IASCTM=        N3*IBUF(49)+N2*IBUF(50)+N1*IBUF(51)+IBUF(52)
      PERIOD= 1.D-4*(N3*IBUF(53)+N2*IBUF(54)+N1*IBUF(55)+IBUF(56))
      ASCLOC= 1.D-3*(N3*IBUF(57)+N2*IBUF(58)+N1*IBUF(59)+IBUF(60))
      ANGINC= 1.D-6*(N3*IBUF(61)+N2*IBUF(62)+N1*IBUF(63)+IBUF(64))
      ANGINC=180.-ANGINC
      AXIS=   1.D-4*(N3*IBUF(65)+N2*IBUF(66)+N1*IBUF(67)+IBUF(68))
      ECC=   1.D-10*(N3*IBUF(69)+N2*IBUF(70)+N1*IBUF(71)+IBUF(72))
      ANGPER= 1.D-5*(N3*IBUF(73)+N2*IBUF(74)+N1*IBUF(75)+IBUF(76))
      ENDIF
C
C     DETERMINE IF ALONGTRACK AND YAW ADJUSTMENT IS NEEDED
C     TRKADJ=-1.2 CORRESPONDS TO A 15 KM ALONGTRACK ADJUSTMENT
C     YAW=.3125 CORRESPONDS TO A 0.5 DEG. YAW ROTATION
C
      TRKADJ=0.
      IF(IOLD.EQ.1.AND.IADJ.EQ.1) TRKADJ=-1.2
      YAW=0.
      IF(ISAT.EQ.8) YAW=.3125
C
C     FIND CELL LATITUDES AND LONGITUDES
C
      J85GHZ=I85GHZ
      IF(TRKADJ.NE.0.) J85GHZ=1
      CALL FDLTLN(J85GHZ,IOLD,YAW)
      IF(TRKADJ.NE.0.) CALL ADJLOC(TRKADJ)
C
C     FIND EITHER TA'S OR TB'S
C
      CALL FDTA(I85GHZ)
      IF(ITB.EQ.1.AND.ISAT.EQ. 8) CALL FDTB08(I85GHZ)
      IF(ITB.EQ.1.AND.ISAT.NE. 8) CALL FDTB00(I85GHZ)
C
      RETURN
      END
C
      SUBROUTINE FDLTLN(J85GHZ,IOLD,YAW)
C
C     THIS SUBROUTINE FINDS THE LATS AND LONS FOR THE SSMI CELLS
C     J85GHZ=0 DOES NOT DO 85 GHZ LAT/LON, J85GHZ=1 DOES 85 GHZ LAT LON
C
      INTEGER*4 INDEX(19),JNDEX(3,109)
C
C     SPECIFY COMMON /INDATA/
C
      INTEGER*2 IBUF
      COMMON /INDATA/ IBUF(1784)
C
C     SPECIFY COMMON /OUTDAT/
C
      REAL*8 REV,XTIME
      INTEGER*4 ITIME,ITIMSC,IVOLT,IAGC,ICOLDA,IHOTA,ICOLDB,IHOTB,IASCTM
      INTEGER*4 IATOIL,IBTOIL,ISAT
      REAL*4 XLATSC,XLONSC,ALTSC,THT,HLTEMP,RFTEMP,FRTEMP
      REAL*4 PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,SPACER 
      REAL*4 ALAT,ALON,BLAT,BLON,TALO,ATAHI,BTAHI
      COMMON/OUTDAT/ REV,XTIME,ITIME,ITIMSC,XLATSC,XLONSC,ALTSC,THT,
     1 HLTEMP(3),IVOLT(2),RFTEMP,FRTEMP,IAGC(6),
     2 IASCTM,PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,ISAT,SPACER(6),
     2 ICOLDA(5,7),IHOTA(5,7),ICOLDB(5,2),IHOTB(5,2),
     3 ALAT(128),ALON(128),BLAT(128),BLON(128),
     4 TALO(5,64),ATAHI(2,128),BTAHI(2,128),IATOIL(128),IBTOIL(128)
C
C     DATA INITIALIZATION
C
      DATA N1/256/
      DATA RAD/0.017453293/
      DATA INDEX/1,9,17,25,33,41,49,57,65,73,81,89,97,105,113,121,123,
     1 127,128/
      DATA JNDEX/
     1   5,  1,  9, 13,  9, 17, 21, 17, 25, 29, 25, 33, 37, 33, 41,
     1  45, 41, 49, 53, 49, 57, 61, 57, 65, 69, 65, 73, 77, 73, 81,
     1  85, 81, 89, 93, 89, 97,101, 97,105,109,105,113,117,113,121,
     1   3,  1,  5,  7,  5,  9, 11,  9, 13, 15, 13, 17, 19, 17, 21,
     1  23, 21, 25, 27, 25, 29, 31, 29, 33, 35, 33, 37, 39, 37, 41,
     1  43, 41, 45, 47, 45, 49, 51, 49, 53, 55, 53, 57, 59, 57, 61,
     1  63, 61, 65, 67, 65, 69, 71, 69, 73, 75, 73, 77, 79, 77, 81,
     1  83, 81, 85, 87, 85, 89, 91, 89, 93, 95, 93, 97, 99, 97,101,
     1 103,101,105,107,105,109,111,109,113,115,113,117,119,117,121,
     1 125,123,127,  2,  1,  3,  4,  3,  5,  6,  5,  7,  8,  7,  9,
     1  10,  9, 11, 12, 11, 13, 14, 13, 15, 16, 15, 17, 18, 17, 19,
     1  20, 19, 21, 22, 21, 23, 24, 23, 25, 26, 25, 27, 28, 27, 29,
     1  30, 29, 31, 32, 31, 33, 34, 33, 35, 36, 35, 37, 38, 37, 39,
     1  40, 39, 41, 42, 41, 43, 44, 43, 45, 46, 45, 47, 48, 47, 49,
     1  50, 49, 51, 52, 51, 53, 54, 53, 55, 56, 55, 57, 58, 57, 59,
     1  60, 59, 61, 62, 61, 63, 64, 63, 65, 66, 65, 67, 68, 67, 69,
     1  70, 69, 71, 72, 71, 73, 74, 73, 75, 76, 75, 77, 78, 77, 79,
     1  80, 79, 81, 82, 81, 83, 84, 83, 85, 86, 85, 87, 88, 87, 89,
     1  90, 89, 91, 92, 91, 93, 94, 93, 95, 96, 95, 97, 98, 97, 99,
     1 100, 99,101,102,101,103,104,103,105,106,105,107,108,107,109,
     1 110,109,111,112,111,113,114,113,115,116,115,117,118,117,119,
     1 120,119,121,122,121,123,124,123,125,126,125,127/
C
C     BEGIN EXECUTION
C
C     SET TABLE LAT/LON FOR A-SCAN
C
      N=-2
      DO 100 JCEL=1,19
      N=N+2
      ICEL=INDEX(JCEL)
      ALAT(ICEL)=0.01*(N1*IBUF(N +263)+IBUF(N +264)-9000)
      ALON(ICEL)=0.01*(N1*IBUF(N +301)+IBUF(N +302))
      IF(ALON(ICEL).GE.360.) ALON(ICEL)=ALON(ICEL)-360.
  100 CONTINUE
C
C     SET MID-POINTS FOR A-SCAN
C
      NCEL=46
      IF(J85GHZ.EQ.1) NCEL=109
      DO 200 JCEL=1,NCEL
      ICEL=JNDEX(1,JCEL)
      I1=JNDEX(2,JCEL)
      I2=JNDEX(3,JCEL)
      DIFLAT=ALAT(I2)-ALAT(I1)
      AVGLAT=0.5*(ALAT(I1)+ALAT(I2))
      DIFLON=ALON(I2)-ALON(I1)
      IF(DIFLON.GT. 180.) DIFLON=DIFLON-360.
      IF(DIFLON.LT.-180.) DIFLON=DIFLON+360.
      AVGLON=ALON(I1)+0.5*DIFLON
      XSQ=(2.*RAD*AVGLAT)**2
      XFAC=1.-0.16627142*XSQ+0.00807934*XSQ*XSQ-0.000151880*XSQ*XSQ*XSQ
      ALAT(ICEL)=AVGLAT*(1.+0.125*(RAD*DIFLON)**2*XFAC)
      X=RAD*(90.-ABS(AVGLAT))
      TANLAT=1./(X+X*X*X/3.)
      IF(AVGLAT.LT.0.) TANLAT=-TANLAT
      ALON(ICEL)=AVGLON-0.2500*RAD*DIFLAT*DIFLON*TANLAT
      IF(ALON(ICEL).LT.  0.) ALON(ICEL)=ALON(ICEL)+360.
      IF(ALON(ICEL).GE.360.) ALON(ICEL)=ALON(ICEL)-360.
  200 CONTINUE
C
C     DO YAW CORRECTION
C
      IF(J85GHZ.EQ.0) THEN
      IF(YAW.EQ.0.) RETURN
C
      DO 260 ICEL=1,127,2
      IF(ICEL.EQ.127) GO TO 250
      DIFLAT=ALAT(ICEL+2)-ALAT(ICEL)
      DIFLON=ALON(ICEL+2)-ALON(ICEL)
      IF(DIFLON.LT.-180.) DIFLON=DIFLON+360.
      IF(DIFLON.GT. 180.) DIFLON=DIFLON-360.
  250 CONTINUE
      ALAT(ICEL)=ALAT(ICEL)+YAW*DIFLAT
      ALON(ICEL)=ALON(ICEL)+YAW*DIFLON
      IF(ALON(ICEL).LT.  0.) ALON(ICEL)=ALON(ICEL)+360.
      IF(ALON(ICEL).GE.360.) ALON(ICEL)=ALON(ICEL)-360.
  260 CONTINUE
      RETURN
      ENDIF
C
C     SET TABLE LAT/LON FOR B-SCAN
C
      N=-2
      DO 300 JCEL=1,19
      N=N+2
      ICEL=INDEX(JCEL)
      IDEL=N1*IBUF(N +339)+IBUF(N +340)
      IF(IDEL.GT.32767) IDEL=IDEL-65536
      LATDEL=(IDEL+30000)/1000-30
      LONDEL=IDEL+29100-1000*(LATDEL+30)
      BLAT(ICEL)=ALAT(ICEL)+0.01*LATDEL
      BLON(ICEL)=ALON(ICEL)+0.01*LONDEL
      IF(BLON(ICEL).LT.  0.) BLON(ICEL)=BLON(ICEL)+360.
      IF(BLON(ICEL).GE.360.) BLON(ICEL)=BLON(ICEL)-360.
  300 CONTINUE
C
C     SET MID-POINTS FOR B-SCAN
C
      DO 400 JCEL=1,109
      ICEL=JNDEX(1,JCEL)
      I1=JNDEX(2,JCEL)
      I2=JNDEX(3,JCEL)
      DIFLAT=BLAT(I2)-BLAT(I1)
      AVGLAT=0.5*(BLAT(I1)+BLAT(I2))
      DIFLON=BLON(I2)-BLON(I1)
      IF(DIFLON.GT. 180.) DIFLON=DIFLON-360.
      IF(DIFLON.LT.-180.) DIFLON=DIFLON+360.
      AVGLON=BLON(I1)+0.5*DIFLON
      XSQ=(2.*RAD*AVGLAT)**2
      XFAC=1.-0.16627142*XSQ+0.00807934*XSQ*XSQ-0.000151880*XSQ*XSQ*XSQ
      BLAT(ICEL)=AVGLAT*(1.+0.125*(RAD*DIFLON)**2*XFAC)
      X=RAD*(90.-ABS(AVGLAT))
      TANLAT=1./(X+X*X*X/3.)
      IF(AVGLAT.LT.0.) TANLAT=-TANLAT
      BLON(ICEL)=AVGLON-0.2500*RAD*DIFLAT*DIFLON*TANLAT
      IF(BLON(ICEL).LT.  0.) BLON(ICEL)=BLON(ICEL)+360.
      IF(BLON(ICEL).GE.360.) BLON(ICEL)=BLON(ICEL)-360.
  400 CONTINUE
C
C     CORRECT CELL 128 FOR DATA BEFORE 1989
C
      IF(IOLD.EQ.1) THEN
      DIFLAT=ALAT(127)-ALAT(126)
      DIFLON=ALON(127)-ALON(126)
      IF(DIFLON.LT.-180.) DIFLON=DIFLON+360.
      IF(DIFLON.GT. 180.) DIFLON=DIFLON-360.
      ALAT(128)=ALAT(127)+DIFLAT
      ALON(128)=ALON(127)+DIFLON
      IF(ALON(128).LT.  0.) ALON(128)=ALON(128)+360.
      IF(ALON(128).GE.360.) ALON(128)=ALON(128)-360.
C
      DIFLAT=BLAT(127)-BLAT(126)
      DIFLON=BLON(127)-BLON(126)
      IF(DIFLON.LT.-180.) DIFLON=DIFLON+360.
      IF(DIFLON.GT. 180.) DIFLON=DIFLON-360.
      BLAT(128)=BLAT(127)+DIFLAT
      BLON(128)=BLON(127)+DIFLON
      IF(BLON(128).LT.  0.) BLON(128)=BLON(128)+360.
      IF(BLON(128).GE.360.) BLON(128)=BLON(128)-360.
      ENDIF
C
C     DO YAW CORRECTION
C
      IF(YAW.EQ.0.) RETURN
      DO 600 ICEL=1,128
      IF(ICEL.EQ.128) GO TO 580
      DIFLAT1=ALAT(ICEL+1)-ALAT(ICEL)
      DIFLON1=ALON(ICEL+1)-ALON(ICEL)
      IF(DIFLON1.LT.-180.) DIFLON1=DIFLON1+360.
      IF(DIFLON1.GT. 180.) DIFLON1=DIFLON1-360.
      DIFLAT2=BLAT(ICEL+1)-BLAT(ICEL)
      DIFLON2=BLON(ICEL+1)-BLON(ICEL)
      IF(DIFLON2.LT.-180.) DIFLON2=DIFLON2+360.
      IF(DIFLON2.GT. 180.) DIFLON2=DIFLON2-360.
  580 CONTINUE
      ALAT(ICEL)=ALAT(ICEL)+2.*YAW*DIFLAT1
      ALON(ICEL)=ALON(ICEL)+2.*YAW*DIFLON1
      IF(ALON(ICEL).LT.  0.) ALON(ICEL)=ALON(ICEL)+360.
      IF(ALON(ICEL).GE.360.) ALON(ICEL)=ALON(ICEL)-360.
      BLAT(ICEL)=BLAT(ICEL)+2.*YAW*DIFLAT2
      BLON(ICEL)=BLON(ICEL)+2.*YAW*DIFLON2
      IF(BLON(ICEL).LT.  0.) BLON(ICEL)=BLON(ICEL)+360.
      IF(BLON(ICEL).GE.360.) BLON(ICEL)=BLON(ICEL)-360.
  600 CONTINUE
      RETURN
      END
C
      SUBROUTINE ADJLOC(TRKADJ)
C
C     CORRECT FOR ALONGTRACK ERROR
C
C     SPECIFY COMMON /OUTDAT/
C
      REAL*8 REV,XTIME
      INTEGER*4 ITIME,ITIMSC,IVOLT,IAGC,ICOLDA,IHOTA,ICOLDB,IHOTB,IASCTM
      INTEGER*4 IATOIL,IBTOIL,ISAT
      REAL*4 XLATSC,XLONSC,ALTSC,THT,HLTEMP,RFTEMP,FRTEMP
      REAL*4 PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,SPACER 
      REAL*4 ALAT,ALON,BLAT,BLON,TALO,ATAHI,BTAHI
      COMMON/OUTDAT/ REV,XTIME,ITIME,ITIMSC,XLATSC,XLONSC,ALTSC,THT,
     1 HLTEMP(3),IVOLT(2),RFTEMP,FRTEMP,IAGC(6),
     2 IASCTM,PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,ISAT,SPACER(6),
     2 ICOLDA(5,7),IHOTA(5,7),ICOLDB(5,2),IHOTB(5,2),
     3 ALAT(128),ALON(128),BLAT(128),BLON(128),
     4 TALO(5,64),ATAHI(2,128),BTAHI(2,128),IATOIL(128),IBTOIL(128)
C
C     BEGIN EXECUTION
C
      DO 100 ICEL=1,128
      DIFLAT=BLAT(ICEL)-ALAT(ICEL)
      ALAT(ICEL)=ALAT(ICEL)+TRKADJ*DIFLAT
      BLAT(ICEL)=BLAT(ICEL)+TRKADJ*DIFLAT
C
      DIFLON=BLON(ICEL)-ALON(ICEL)
      IF(DIFLON.LT.-180.) DIFLON=DIFLON+360.
      IF(DIFLON.GT. 180.) DIFLON=DIFLON-360.
C
      ALON(ICEL)=ALON(ICEL)+TRKADJ*DIFLON
      IF(ALON(ICEL).LT.  0.) ALON(ICEL)=ALON(ICEL)+360.
      IF(ALON(ICEL).GE.360.) ALON(ICEL)=ALON(ICEL)-360.
C
      BLON(ICEL)=BLON(ICEL)+TRKADJ*DIFLON
      IF(BLON(ICEL).LT.  0.) BLON(ICEL)=BLON(ICEL)+360.
      IF(BLON(ICEL).GE.360.) BLON(ICEL)=BLON(ICEL)-360.
  100 CONTINUE
      RETURN
      END
C
      SUBROUTINE FDTA(I85GHZ)
C
C     THIS SUBROUTINE FINDS THE ANTENNA TEMPERATURES AND SURFACE TYPES
C
      INTEGER*4 ITALO(5),ITAHI(8)
C
C     SPECIFY COMMON /INDATA/
C
      INTEGER*2 IBUF
      COMMON /INDATA/ IBUF(1784)
C
C     SPECIFY COMMON /OUTDAT/
C
      REAL*8 REV,XTIME
      INTEGER*4 ITIME,ITIMSC,IVOLT,IAGC,ICOLDA,IHOTA,ICOLDB,IHOTB,IASCTM
      INTEGER*4 IATOIL,IBTOIL,ISAT
      REAL*4 XLATSC,XLONSC,ALTSC,THT,HLTEMP,RFTEMP,FRTEMP
      REAL*4 PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,SPACER
      REAL*4 ALAT,ALON,BLAT,BLON,TALO,ATAHI,BTAHI
      COMMON/OUTDAT/ REV,XTIME,ITIME,ITIMSC,XLATSC,XLONSC,ALTSC,THT,
     1 HLTEMP(3),IVOLT(2),RFTEMP,FRTEMP,IAGC(6),
     2 IASCTM,PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,ISAT,SPACER(6),
     2 ICOLDA(5,7),IHOTA(5,7),ICOLDB(5,2),IHOTB(5,2),
     3 ALAT(128),ALON(128),BLAT(128),BLON(128),
     4 TALO(5,64),ATAHI(2,128),BTAHI(2,128),IATOIL(128),IBTOIL(128)
C
      DATA N1,N2/256,65536/
C
C     BEGIN EXECUTION
C
      N=-10
      M=-12
C
      DO 100 ICEL=1,64
      JCEL=2*ICEL-1
C
C     FIND THE TA'S FOR THE 3 LOWER FREQUENCIES
C
      N=N+10
C
      IWORK4=N2*IBUF(N +377)+N1*IBUF(N+378)+IBUF(N+379)
      ITAV=INT(IWORK4/4096)
      ITALO(1)=ITAV
      ITALO(2)=IWORK4-4096*ITAV
C
      IWORK4=N2*IBUF(N +380)+N1*IBUF(N+381)+IBUF(N+382)
      ITAV=INT(IWORK4/4096)
      ITALO(4)=ITAV
      ITALO(5)=IWORK4-4096*ITAV
C
      IWORK4=N2*IBUF(N +383)+N1*IBUF(N+384)+IBUF(N+385)
      ITAV=INT(IWORK4/4096)
      ITALO(3)=ITAV
C
C     SCALE THE TA'S
C
      DO 20 ICH=1,5
      IF(ITALO(ICH).LE.3800) THEN
      TALO(ICH,ICEL)=0.1*ITALO(ICH)
      ELSE
      TALO(ICH,ICEL)=ITALO(ICH)-3420
      ENDIF
   20 CONTINUE
C
C     FIND THE TOIL FLAGS
C
      IRES=IWORK4-4096*ITAV
      ITOIL1=INT(IRES/512)
      IATOIL(JCEL)=ITOIL1
C
      IF(I85GHZ.EQ.0) GO TO 100
C
C     FIND 85 GHZ TOILS'S AND TA'S
C
      M=M+12
C
      IRES=IRES-ITOIL1*512
      ITOIL2=INT(IRES/64)
      IRES=IRES-ITOIL2*64
      ITOIL3=INT(IRES/8)
      ITOIL4=IRES-ITOIL3*8
      IBTOIL(JCEL)=ITOIL2
      IATOIL(JCEL+1)=ITOIL3
      IBTOIL(JCEL+1)=ITOIL4
C
      IWORK4=N2*IBUF(M+1017)+N1*IBUF(M+1018)+IBUF(M+1019)
      ITAV=INT(IWORK4/4096)
      ITAHI(1)=ITAV
      ITAHI(3)=IWORK4-4096*ITAV
C
      IWORK4=N2*IBUF(M+1020)+N1*IBUF(M+1021)+IBUF(M+1022)
      ITAV=INT(IWORK4/4096)
      ITAHI(2)=ITAV
      ITAHI(4)=IWORK4-4096*ITAV
C
      IWORK4=N2*IBUF(M+1023)+N1*IBUF(M+1024)+IBUF(M+1025)
      ITAV=INT(IWORK4/4096)
      ITAHI(5)=ITAV
      ITAHI(7)=IWORK4-4096*ITAV
C
      IWORK4=N2*IBUF(M+1026)+N1*IBUF(M+1027)+IBUF(M+1028)
      ITAV=INT(IWORK4/4096)
      ITAHI(6)=ITAV
      ITAHI(8)=IWORK4-4096*ITAV
C
C     SCALE THE TA'S
C
      K=0
      DO 40 KCEL=JCEL,JCEL+1
      DO 40 ICH=1,2
      K=K+1
      IF(ITAHI(K).LE.3800) THEN
      ATAHI(ICH,KCEL)=0.1*ITAHI(K)
      ELSE
      ATAHI(ICH,KCEL)=ITAHI(K)-3420
      ENDIF
C
      K=K+1
C
      IF(ITAHI(K).LE.3800) THEN
      BTAHI(ICH,KCEL)=0.1*ITAHI(K)
      ELSE
      BTAHI(ICH,KCEL)=ITAHI(K)-3420
      ENDIF
C
   40 CONTINUE
C
  100 CONTINUE
      RETURN
      END
C
      SUBROUTINE FDTB08(I85GHZ)
C
C     THIS SUBROUTINE CONVERTS ANTENNA TEMPS. TO BRIGHTNESS TEMPS.
C     F08 ALONG-SCAN AND OFFSET CORRECTIONS
C
      REAL*4 DELTA(4),CHI(2,4),AVGTA(5),ABIAS(5)
      REAL*4 SBIAS1(64),SBIAS2(64),SBIAS3(64),SBIAS4(64),SBIAS5(64)
      REAL*4 AVV(4),AHV(4),AOV(4),AHH(4),AVH(4),AOH(4)
C
C     SPECIFY COMMON /OUTDAT/
C
      REAL*8 REV,XTIME
      INTEGER*4 ITIME,ITIMSC,IVOLT,IAGC,ICOLDA,IHOTA,ICOLDB,IHOTB,IASCTM
      INTEGER*4 IATOIL,IBTOIL,ISAT
      REAL*4 XLATSC,XLONSC,ALTSC,THT,HLTEMP,RFTEMP,FRTEMP
      REAL*4 PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,SPACER
      REAL*4 ALAT,ALON,BLAT,BLON,TALO,ATAHI,BTAHI
      COMMON/OUTDAT/ REV,XTIME,ITIME,ITIMSC,XLATSC,XLONSC,ALTSC,THT,
     1 HLTEMP(3),IVOLT(2),RFTEMP,FRTEMP,IAGC(6),
     2 IASCTM,PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,ISAT,SPACER(6),
     2 ICOLDA(5,7),IHOTA(5,7),ICOLDB(5,2),IHOTB(5,2),
     3 ALAT(128),ALON(128),BLAT(128),BLON(128),
     4 TALO(5,64),ATAHI(2,128),BTAHI(2,128),IATOIL(128),IBTOIL(128)
C
C     DATA INITIALIZATION
C
      DATA ISTART/1/
      DATA DELTA/0.03199,0.02685,0.01434,0.01186/
      DATA CHI/.00379,.00525,.00983,0.0,.02136,.02664,.01387,.01967/
      DATA AVGTA/190.93,130.14,215.42,211.39,158.16/
C
C     **********************************************************
      DATA ABIAS/5*0./
C     IN ORDER TO APPLY WENTZ [1992] OFFSET TO TB'S,
C     ACTIVATE NEXT STATEMENT AND COMMENT OUT PREVIOUS STATEMENT 
C     DATA ABIAS/2.0,3.5,1.32,-1.57,-0.22/
C     **********************************************************
C
      DATA SBIAS1/                          -.03,  .01,  .04,  .06,
     +   .09,  .10,  .12,  .12,  .13,  .13,  .13,  .13,  .13,  .14,
     +   .14,  .14,  .14,  .14,  .15,  .15,  .14,  .14,  .13,  .12,
     +   .12,  .11,  .11,  .11,  .11,  .11,  .12,  .11,  .10,  .10,
     +   .09,  .07,  .06,  .05,  .05,  .04,  .03,  .03,  .02,  .01,
     +  -.01, -.02, -.04, -.05, -.06, -.08, -.09, -.09, -.11, -.12,
     +  -.14, -.18, -.21, -.26, -.30, -.36, -.43, -.52, -.63, -.75/
      DATA SBIAS2/                          -.03, -.01,  .00,  .01,
     +   .01,  .01,  .01,  .01,  .01,  .01,  .01,  .02,  .03,  .04,
     +   .04,  .05,  .05,  .05,  .05,  .05,  .04,  .04,  .04,  .04,
     +   .04,  .04,  .04,  .04,  .04,  .05,  .06,  .07,  .07,  .08,
     +   .07,  .07,  .06,  .07,  .07,  .06,  .06,  .06,  .06,  .06,
     +   .05,  .04,  .04,  .04,  .04,  .02,  .00, -.01, -.02, -.03,
     +  -.04, -.06, -.09, -.12, -.16, -.20, -.26, -.32, -.39, -.48/
      DATA SBIAS3/                          -.05,  .00,  .04,  .07,
     +   .08,  .09,  .10,  .11,  .11,  .12,  .12,  .13,  .14,  .14,
     +   .15,  .15,  .17,  .17,  .16,  .16,  .15,  .14,  .14,  .14,
     +   .13,  .13,  .13,  .13,  .13,  .13,  .14,  .13,  .14,  .13,
     +   .12,  .11,  .10,  .09,  .09,  .08,  .07,  .05,  .04,  .03,
     +   .02,  .00, -.01, -.02, -.04, -.05, -.07, -.10, -.12, -.14,
     +  -.17, -.20, -.25, -.31, -.36, -.42, -.50, -.63, -.79, -.97/
      DATA SBIAS4/                          -.06, -.03,  .00,  .01,
     +   .03,  .04,  .05,  .06,  .06,  .07,  .09,  .10,  .11,  .13,
     +   .14,  .15,  .15,  .16,  .16,  .16,  .16,  .16,  .16,  .15,
     +   .14,  .14,  .14,  .13,  .13,  .13,  .14,  .13,  .13,  .13,
     +   .12,  .11,  .10,  .10,  .09,  .08,  .08,  .06,  .06,  .05,
     +   .04,  .03,  .02,  .01,  .00, -.02, -.05, -.07, -.09, -.12,
     +  -.16, -.18, -.22, -.27, -.33, -.42, -.50, -.60, -.74, -.90/
      DATA SBIAS5/                          -.23, -.20, -.17, -.15,
     +  -.14, -.13, -.12, -.10, -.09, -.10, -.09, -.07, -.05, -.04,
     +  -.03,  .00,  .01,  .03,  .04,  .04,  .04,  .05,  .05,  .05,
     +   .04,  .04,  .04,  .05,  .05,  .07,  .08,  .09,  .10,  .11,
     +   .10,  .10,  .09,  .10,  .10,  .09,  .09,  .09,  .09,  .08,
     +   .07,  .07,  .07,  .09,  .09,  .08,  .08,  .08,  .08,  .08,
     +   .06,  .04,  .00, -.03, -.07, -.11, -.16, -.22, -.29, -.41/
C
C     BEGIN EXECUTION
C
      IF(ISTART.EQ.0) GO TO 30
      ISTART=0
      DO 10 IFREQ=1,4
      IF(IFREQ.EQ.2) GO TO 10
      XFAC=(1.-CHI(1,IFREQ)*CHI(2,IFREQ))*(1.-DELTA(IFREQ))
      AVV(IFREQ)=(1.+CHI(1,IFREQ))/XFAC
      AHV(IFREQ)=-CHI(1,IFREQ)*(1.+CHI(2,IFREQ))/XFAC
      AOV(IFREQ)=(1.-AVV(IFREQ)-AHV(IFREQ))*2.7
      AHH(IFREQ)=(1.+CHI(2,IFREQ))/XFAC
      AVH(IFREQ)=-CHI(2,IFREQ)*(1.+CHI(1,IFREQ))/XFAC
      AOH(IFREQ)=(1.-AHH(IFREQ)-AVH(IFREQ))*2.7
   10 CONTINUE
C
      DO 20 ICEL=1,64
      SBIAS1(ICEL)=1.-SBIAS1(ICEL)/AVGTA(1)
      SBIAS2(ICEL)=1.-SBIAS2(ICEL)/AVGTA(2)
      SBIAS3(ICEL)=1.-SBIAS3(ICEL)/AVGTA(3)
      SBIAS4(ICEL)=1.-SBIAS4(ICEL)/AVGTA(4)
      SBIAS5(ICEL)=1.-SBIAS5(ICEL)/AVGTA(5)
   20 CONTINUE
C
   30 CONTINUE
C
      DO 100 ICEL=1,64
      TA19V=TALO(1,ICEL)*SBIAS1(ICEL)-ABIAS(1)
      TA19H=TALO(2,ICEL)*SBIAS2(ICEL)-ABIAS(2)
      TA22V=TALO(3,ICEL)*SBIAS3(ICEL)-ABIAS(3)
      TA37V=TALO(4,ICEL)*SBIAS4(ICEL)-ABIAS(4)
      TA37H=TALO(5,ICEL)*SBIAS5(ICEL)-ABIAS(5)
C
      TALO(1,ICEL)=AVV(1)*TA19V+AHV(1)*TA19H+AOV(1)
      TALO(2,ICEL)=AHH(1)*TA19H+AVH(1)*TA19V+AOH(1)
      TALO(3,ICEL)=1.01993*TA22V+1.994
      TALO(4,ICEL)=AVV(3)*TA37V+AHV(3)*TA37H+AOV(3)
      TALO(5,ICEL)=AHH(3)*TA37H+AVH(3)*TA37V+AOH(3)
  100 CONTINUE
C
      IF(I85GHZ.EQ.0) RETURN
C
      DO 200 ICEL=1,128
      TB85V=AVV(4)*ATAHI(1,ICEL)+AHV(4)*ATAHI(2,ICEL)+AOV(4)
      TB85H=AHH(4)*ATAHI(2,ICEL)+AVH(4)*ATAHI(1,ICEL)+AOH(4)
      ATAHI(1,ICEL)=TB85V
      ATAHI(2,ICEL)=TB85H
      TB85V=AVV(4)*BTAHI(1,ICEL)+AHV(4)*BTAHI(2,ICEL)+AOV(4)
      TB85H=AHH(4)*BTAHI(2,ICEL)+AVH(4)*BTAHI(1,ICEL)+AOH(4)
      BTAHI(1,ICEL)=TB85V
      BTAHI(2,ICEL)=TB85H
  200 CONTINUE
      RETURN
      END
C
      SUBROUTINE FDTB00(I85GHZ)
C
C     THIS SUBROUTINE CONVERTS ANTENNA TEMPS. TO BRIGHTNESS TEMPS.
C     ALONG-SCAN AND OFFSET CORRECTIONS SET TO ZERO
C
      REAL*4 DELTA(4),CHI(2,4),AVGTA(5),ABIAS(5)
      REAL*4 SBIAS1(64),SBIAS2(64),SBIAS3(64),SBIAS4(64),SBIAS5(64)
      REAL*4 AVV(4),AHV(4),AOV(4),AHH(4),AVH(4),AOH(4)
C
C     SPECIFY COMMON /OUTDAT/
C
      REAL*8 REV,XTIME
      INTEGER*4 ITIME,ITIMSC,IVOLT,IAGC,ICOLDA,IHOTA,ICOLDB,IHOTB,IASCTM
      INTEGER*4 IATOIL,IBTOIL,ISAT
      REAL*4 XLATSC,XLONSC,ALTSC,THT,HLTEMP,RFTEMP,FRTEMP
      REAL*4 PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,SPACER
      REAL*4 ALAT,ALON,BLAT,BLON,TALO,ATAHI,BTAHI
      COMMON/OUTDAT/ REV,XTIME,ITIME,ITIMSC,XLATSC,XLONSC,ALTSC,THT,
     1 HLTEMP(3),IVOLT(2),RFTEMP,FRTEMP,IAGC(6),
     2 IASCTM,PERIOD,ASCLOC,ANGINC,AXIS,ECC,ANGPER,ISAT,SPACER(6),
     2 ICOLDA(5,7),IHOTA(5,7),ICOLDB(5,2),IHOTB(5,2),
     3 ALAT(128),ALON(128),BLAT(128),BLON(128),
     4 TALO(5,64),ATAHI(2,128),BTAHI(2,128),IATOIL(128),IBTOIL(128)
C
C     DATA INITIALIZATION
C
      DATA ISTART/1/
      DATA DELTA/0.03199,0.02685,0.01434,0.01186/
      DATA CHI/.00379,.00525,.00983,0.0,.02136,.02664,.01387,.01967/
      DATA AVGTA/190.93,130.14,215.42,211.39,158.16/
      DATA ABIAS/5*0./
      DATA SBIAS1/64*0./
      DATA SBIAS2/64*0./
      DATA SBIAS3/64*0./
      DATA SBIAS4/64*0./
      DATA SBIAS5/64*0./
C
C     BEGIN EXECUTION
C
      IF(ISTART.EQ.0) GO TO 30
      ISTART=0
      DO 10 IFREQ=1,4
      IF(IFREQ.EQ.2) GO TO 10
      XFAC=(1.-CHI(1,IFREQ)*CHI(2,IFREQ))*(1.-DELTA(IFREQ))
      AVV(IFREQ)=(1.+CHI(1,IFREQ))/XFAC
      AHV(IFREQ)=-CHI(1,IFREQ)*(1.+CHI(2,IFREQ))/XFAC
      AOV(IFREQ)=(1.-AVV(IFREQ)-AHV(IFREQ))*2.7
      AHH(IFREQ)=(1.+CHI(2,IFREQ))/XFAC
      AVH(IFREQ)=-CHI(2,IFREQ)*(1.+CHI(1,IFREQ))/XFAC
      AOH(IFREQ)=(1.-AHH(IFREQ)-AVH(IFREQ))*2.7
   10 CONTINUE
C
      DO 20 ICEL=1,64
      SBIAS1(ICEL)=1.-SBIAS1(ICEL)/AVGTA(1)
      SBIAS2(ICEL)=1.-SBIAS2(ICEL)/AVGTA(2)
      SBIAS3(ICEL)=1.-SBIAS3(ICEL)/AVGTA(3)
      SBIAS4(ICEL)=1.-SBIAS4(ICEL)/AVGTA(4)
      SBIAS5(ICEL)=1.-SBIAS5(ICEL)/AVGTA(5)
   20 CONTINUE
C
   30 CONTINUE
C
      DO 100 ICEL=1,64
      TA19V=TALO(1,ICEL)*SBIAS1(ICEL)-ABIAS(1)
      TA19H=TALO(2,ICEL)*SBIAS2(ICEL)-ABIAS(2)
      TA22V=TALO(3,ICEL)*SBIAS3(ICEL)-ABIAS(3)
      TA37V=TALO(4,ICEL)*SBIAS4(ICEL)-ABIAS(4)
      TA37H=TALO(5,ICEL)*SBIAS5(ICEL)-ABIAS(5)
C
      TALO(1,ICEL)=AVV(1)*TA19V+AHV(1)*TA19H+AOV(1)
      TALO(2,ICEL)=AHH(1)*TA19H+AVH(1)*TA19V+AOH(1)
      TALO(3,ICEL)=1.01993*TA22V+1.994
      TALO(4,ICEL)=AVV(3)*TA37V+AHV(3)*TA37H+AOV(3)
      TALO(5,ICEL)=AHH(3)*TA37H+AVH(3)*TA37V+AOH(3)
  100 CONTINUE
C
      IF(I85GHZ.EQ.0) RETURN
C
      DO 200 ICEL=1,128
      TB85V=AVV(4)*ATAHI(1,ICEL)+AHV(4)*ATAHI(2,ICEL)+AOV(4)
      TB85H=AHH(4)*ATAHI(2,ICEL)+AVH(4)*ATAHI(1,ICEL)+AOH(4)
      ATAHI(1,ICEL)=TB85V
      ATAHI(2,ICEL)=TB85H
      TB85V=AVV(4)*BTAHI(1,ICEL)+AHV(4)*BTAHI(2,ICEL)+AOV(4)
      TB85H=AHH(4)*BTAHI(2,ICEL)+AVH(4)*BTAHI(1,ICEL)+AOH(4)
      BTAHI(1,ICEL)=TB85V
      BTAHI(2,ICEL)=TB85H
  200 CONTINUE
      RETURN
      END
