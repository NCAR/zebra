      SUBROUTINE DPSINO(IPREC,X,NSCALE,N,ISTAT)
C *** McIDAS Revision History ***
C 1 DPSINO.FOR 23-Mar-90,15:20:58,`SSEC' PC-McIDAS ver 5.00
C 2 DPSINO.FOR 24-Sep-90,18:21:26,`SMG' First Release into COMmon
C *** McIDAS Revision History ***
C $ CALL DPSINO(IPREC, X, NSCALE, N, ISTAT)  (DAS)
C $ CONVERT REAL*8 TO SCALED INTEGER IN NSCALE & N.
C $ INPUT:
C $   IPREC = (I) PRECISION (FOR 32 BIT WORD, 1...9)
C $   X = (R*8) DOUBLE PRECISION NUMBER TO CONVERT
C $ OUTPUT:
C $   NSCALE = (I) # OF IMPLIED DECIMAL PLACES IN N
C $   N = (I) (NORMALIZED) INTEGER EXPRESSING VALUE OF X
C $   ISTAT = (I) 0 (OK), OR -1 (ERROR: BAD IPREC, OR VALUE IN X CAUSES
C $      NSCALE TO HAVE ABS. VAL. .GT.99)
C $$ DPSINO = CONVERT, REAL, INTEGER
C-----EXAMPLES: (PREC. 4) .9723D1 --> 3,9723
C            (PREC 4) .9723D-99 --> (ERROR: NSCALE WOULD BE < -99)
C            (PREC 3) -.9728D5 --> -2,-973  (NOTICE  ROUNDING OF -973)
C            (PREC 7) 1.D0 --> 6,1000000
C
      IMPLICIT REAL*8 (X-Z)
      DOUBLE PRECISION XMAG(09)
C-----MXPREC GIVES MAX PRECISION (# OF DEC DIGITS) OF COMPUTER
C        WORD
      DATA MXPREC/09/
      DATA XMAG/1.D0,1.D1,1.D2,1.D3,1.D4,1.D5,1.D6,1.D7,1.D8/
C
C
C
      IF (IPREC.LT.1.OR.IPREC.GT.MXPREC) GOTO 91
C
C-----NORMALIZE VALUE TO HAVE FIRST SIG. DIGIT JUST TO RIGHT OF
C        DECIMAL POINT
C
      NSCALE=0
      XVAL=DABS(X)
      IF (XVAL.EQ.0.D0) GOTO 10
      XLOW=XMAG(IPREC)
      XHI=XLOW*10.D0
      DO 8 J=1,99
         IF (XVAL.LT.XHI) GOTO 4
            XVAL=XVAL*.1D0
            NSCALE=NSCALE-1
            GOTO 8
 4       IF (XVAL.GE.XLOW) GOTO 10
            XVAL=XVAL*10.D0
            NSCALE=NSCALE+1
            GOTO 8
 8    CONTINUE
      GOTO 91
C
C-----ROUND NORMALIZED VALUE
C
 10   Y=XVAL+.5D0
      IF (Y.LT.XHI) GOTO 20
         Y=Y*.1D0
         NSCALE=NSCALE-1
         IF (NSCALE.LT.-99) GOTO 91
 20   N=IDINT(Y)
      IF (X.LT.0.D0) N=-N
      IF (N.EQ.0) NSCALE=0
      ISTAT=0
      RETURN
 91   N=0
      NSCALE=0
      ISTAT=-1
      RETURN
      END
