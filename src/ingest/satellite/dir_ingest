#!/bin/csh
#
# Ingest a directory worth of STORM-FEST satellite and nowrad files
#
#
# build a simple data store config file
#
	cat > temp.config << _EOF_
		set DataDir "/scr/scupper/fest"

		platform goes
			organization	image
			filetype	raster
			maxsamples	5
		endplatform

		platform nowrad
			organization	image
			filetype	raster
			maxsamples	5
		endplatform

		platform nowrad_f
			organization	image
			filetype	raster
			maxsamples	5
		endplatform
_EOF_
#
# start necessary zeb stuff
#
	setenv XAPPLRESDIR /zeb/lib/resources
	setenv PATH ${PATH}:/zeb/bin
	setenv HOST `hostname`
	zebstop
	rm -f /tmp/fcc.socket
	sleep 1
	set nonomatch; rm -f /zeb/tmp/*
	message & 
	sleep 1
	mstatus > /dev/null
	EventLogger &
	sleep 1
	timer &
	dsDaemon temp.config &
	sleep 5
#
# build a template of the satellite ingest stuff that stays the same
#
	cat > sat_template << _EOF_
		limits 33 -106 44 -89
		set originLat 39.71
		set platform "goes"
_EOF_

#
# loop through the time indices
#
	if ($1) then
		set i = $1
	else
		set i = 0
	endif

	while ($i < 96)
	#
	# Grab visible data when 56 <= i <= 92 (14:00 - 23:00 GMT)
	#
		@ vis = ($i >= 56 && $i <= 92)
	#
	# Add a leading zero if necessary
	#
		if ($i < 10) set i = 0$i
	#
	# Odd day's GOES files
	#
		cp sat_template cmdfile

		if ($vis) then
			echo "		file area20$i vis"	>> cmdfile
			echo "		set kmResolution 4"	>> cmdfile
		else
			echo "		file area21$i ir"	>> cmdfile
			echo "		set kmResolution 8"	>> cmdfile
		endif

		if (($vis && -e area20$i) || (! $vis && -e area21$i)) then
			echo -n "area20${i}/21${i}: "

			/zeb/ingest/satellite/newSatIngest cmdfile

			if (${status} == 0) then
				echo "success"
			else
				echo "FAILURE"
			endif
		endif
	#
	# Same as above for even day's GOES files
	#
		cp sat_template cmdfile

		if ($vis) then
			echo "		file area30$i vis"	>> cmdfile
			echo "		set kmResolution 4"	>> cmdfile
		else
			echo "		file area31$i ir"	>> cmdfile
			echo "		set kmResolution 8"	>> cmdfile
		endif

		if (($vis && -e area30$i) || (! $vis && -e area31$i)) then
			echo -n "area30${i}/31${i}: "

			/zeb/ingest/satellite/newSatIngest cmdfile

			if (${status} == 0) then
				echo "success"
			else
				echo "FAILURE"
			endif
		endif
	#
	# Odd day's WSI nowrad, fixed and floater
	#
		if (-e area25$i) then
			echo -n "area25${i}: "

			/zeb/ingest/nowrad/NowradIngest nowrad area25$i 39.71

			if (${status} == 0) then
				echo "success"
			else
				echo "FAILURE"
			endif
		endif

		if (-e area26$i) then
			echo -n "area26${i}: "

			/zeb/ingest/nowrad/NowradIngest nowrad_f area26$i 39.71

			if (${status} == 0) then
				echo "success"
			else
				echo "FAILURE"
			endif
		endif
	#
	# Same as above for even day
	#
		if (-e area35$i) then
			echo -n "area35${i}: "

			/zeb/ingest/nowrad/NowradIngest nowrad area35$i 39.71

			if (${status} == 0) then
				echo "success"
			else
				echo "FAILURE"
			endif
		endif

		if (-e area36$i) then
			echo -n "area36${i}: "

			/zeb/ingest/nowrad/NowradIngest nowrad_f area36$i 39.71

			if (${status} == 0) then
				echo "success"
			else
				echo "FAILURE"
			endif
		endif
	#
	# increment time index
	#
		@ i = $i + 1
	end


#
# remove the config file, template, and the last command file
#
rm temp.config
rm sat_template
rm cmdfile
