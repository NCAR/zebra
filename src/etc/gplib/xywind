!
! Wind profile stuff used by the wind-template.
!
! This module 'extends' the time-height module with a menu and widgets
! for tweaking parameters specific to xywind plots.

require simple-color
require time-height
require dataskip
require form-utilities
require color-table

!
! The menu meant to encapsulate all of the XYWind parameters for a 
! wind profile component of a time-height plot.
!
define widget time-height-winds intmenu 'Generic winds'
	title 'Time-Height Winds'
	line
	entry 'Vector' \
		'parameter #icon_component representation-style vector' \
	(PDSearch (icon_component,"representation-style","xy-wind") <> "barb")
	entry 'Barb' 'parameter #icon_component representation-style barb' \
	(PDSearch (icon_component,"representation-style","xy-wind") = "barb")
	entry 'Barb in knots' \
		'parameter #icon_component barb-type knots' \
           (PDSearch (icon_component,"barb-type","xy-wind") = "knots")
	entry 'Barb in m/s' \
		'parameter #icon_component barb-type "m/s"' \
           (PDSearch (icon_component,"barb-type","xy-wind") <> "knots")
	line
	entry 'Color-coded barbs/arrows' 'ToggleBoolean color-mono' \
		(PDParam (icon_component, 'color-mono') <> 'true')
	submenu 'Color table' Color-tables
	submenu 'Barb/arrow color (when mono)' simple-color
	entry 'Wind parameters...' 'popup xyw-parameters'
	line
	submenu 'Data skip interval' 'data-skip-menu'
	line
	entry 'Global grid parameters...' 'popup th-global'
	entry 'Select height field...' 'selectfield "param=y-field"'
endmenu



!
! big damn form widget to tweak all these parameters.
!
define widget xyw-parameters form 'Wind parameters'
	noheader
	popup 'xyw-popup'

	label title 'Wind profile display parameters'
	button help 'Help' command 'help limits'
	button apply 'Apply' command 'xyw-apply'
	button dismiss 'Dismiss' color red command 'popdown #ui$form'
	label comp 'x' width 1 color gray95
	newline

	label l6 'Vector scale:'
	blank vscale 'x' width 60 command 'xyw-apply'
	label l7 'Barb shaft length:'
	blank bscale 'x' width 60 command 'xyw-apply'
	newline

	label lmono 'Color coding: '
	button mono 'unknown' command 'fu-toggle-enabled' width 80
	label lstep ' Color step: '
	blank step '0.0' width 60 command 'xyw-apply'
enddef


procedure xyw-popup
	local f ui$form
	local c icon_component
	formtext #f comp #c

	local v PDSearch(c,'vec-scale','xy-wind')
	formtext #f vscale #v
	local b PDSearch(c,'barb-scale','xy-wind')
	formtext #f bscale #b
	fu-setdisabled #f mono #c 'color-mono' False
	local value PDSearch(icon_component, 'step', 'xy-wind')
	if (value = PUNDEF)
		formtext #f step '5.0'
	else
		formtext #f step #value
	endif
endprocedure


procedure xyw-apply
	local f ui$form
	local c getftext(f, 'comp')

	local v getftext(f, 'vscale')
	parameter #c vec-scale #v
	local b getftext(f, 'bscale')
	parameter #c barb-scale #b
	fu-applydisabled #f 'mono' #c 'color-mono'
	local step getftext(f, 'step')
	parameter #c step #step
endprocedure


