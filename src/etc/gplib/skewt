!
! Skew-t support stuff.
!
!
! The Skewt Adjust Limits submenu
!
define widget adjust-skewt-limits intmenu 'Adjust Limits'
	title 'Adjust Limits'
	line
	entry 'Pressure' 'i_adjust pres-limit-proc'
	entry 'Temperature' 'i_adjust temp-limit-proc'
endmenu

!
! The Skew-t left button menu.
!
define widget skewt-left intmenu 'Skewt left'
	title 'Overlay Operations'
	line
	submenu 'Adjust Limits' 'adjust-skewt-limits'
	line
	entry 'Raise to top' 'move #icon_component 0'
	entry 'Move to bottom' 'move #icon_component 1'
	entry 'Remove' 'i_remove_component'
endmenu

!
! These are the procedures for the usual limit-proc names given in the
! defaults pd, and they are relatively ubiquitous for skewt plots, so
! define them here in case they haven't been included elsewhere.  To
! override these, someone would have to use a different name and change the
! limit-proc parameters in defaults.
!
procedure adj_temp_minmax comp string
    local min PDSearch(comp, "temp-minval", "skewt")
    local max PDSearch(comp, "temp-maxval", "skewt")
    activate doublefloat #comp \
	"temp-minval" "Temperature Min: " #min \
	"temp-maxval" "Temperature Max: " #max
endprocedure

procedure adj_pres_minmax comp string
    local min PDSearch(comp, "pres-minval", "skewt")
    local max PDSearch(comp, "pres-maxval", "skewt")
    activate doublefloat #comp \
	"pres-minval" "Pressure Min: " #min \
	"pres-maxval" "Pressure Max: " #max
endprocedure


!
! Menu for top annotation.
!
define widget skewt-topannot intmenu 'stuff'
	title 'Skew-t platforms'
	line
	entry 'Remove this platform' 'skewt-remove'
endmenu

procedure skewt-remove 
	local plats PDParam(icon_component,'platform')
!
! If we are the only platform we really want to substitute "null".  Kludge.
!
	if (plats = icon_platform)
	   	parameter #icon_component platform null
		return
	endif
!
! Nope, there are others.  Just clean this one out.
!
	local pos ListPosition(icon_platform, plats)
	local plats quote(RmElement(plats,pos))
	parameter #icon_component platform #plats
endprocedure


!
! Menu for changing time offsets from top annotation
!
define widget skewt-topannot-offsets intmenu 'offsets'
	title 'Time Offsets'
	line
	entry 'Reset all offsets to 0' 'skewt-reset-offsets'
	entry '0, 24, 48 hours' 'skewt-set-offset 0h,24h,48h'
	entry '0, 12, 24 hours' 'skewt-set-offset 0h,12h,24h'
	entry '0, 6, 12 hours' 'skewt-set-offset 0h,6h,12h'
	entry '0, 3, 6 hours' 'skewt-set-offset 0h,3h,6h'
	entry '3, 6, 9 hours' 'skewt-set-offset 3h,6h,9h'
	entry '6, 12, 18 hours' 'skewt-set-offset 6h,12h,18h'
endmenu

procedure skewt-reset-offsets
!	local dontcare pd_removeparam(icon_component,"time-offset")
	parameter #icon_component "time-offset" "0,0,0"
endprocedure

procedure skewt-set-offset offset string
	parameter #icon_component "time-offset" #offset
endprocedure

!
! Various skewt params.
!
define widget skewt-right intmenu 'feet/km'
	title 'Skew-t modes'
	line
	entry 'Times in top annotation' 'ToggleBoolean "annot-time"' \
		(PDParam (icon_component, 'annot-time') <> 'false')
	entry 'Overlay new soundings' 'ToggleBoolean "overlay-mode"' \
	        (PDParam (icon_component, 'overlay-mode') <> 'false')
	entry 'Wind barbs' 'skewt-toggle-wind' \
		(PDParam (icon_component, 'wind-style') = 'barb')
	line
	entry 'Altitude in feet' 'do-feet-proc true' \
		(PDParam (icon_component, 'do-feet') = 'true')
	entry 'Altitude in kilometers' 'do-feet-proc false' \
		(PDParam (icon_component, 'do-feet') <> 'true')
	line
	submenu 'Data skip interval' 'skewt-data-skip'
endmenu

procedure do-feet-proc value string
	parameter #icon_component do-feet #value
endprocedure

procedure skewt-toggle-wind
	local style PDParam(icon_component,"wind-style")
	if (style = "barb")
		parameter #icon_component wind-style vector
	else
		parameter #icon_component wind-style barb
	endif
endprocedure

define widget skewt-data-skip intmenu 'skip data points'
	title 'Data skip interval'
	line
	entry 'Plot every point' \
		'parameter #icon_component data-skip "1"' \
		(PDParam (icon_component,"data-skip") = "1")
	entry 'Every 5th point' \
		'parameter #icon_component data-skip "5"' \
		(PDParam (icon_component,"data-skip") = "5")
	entry 'Every 10th point' \
		'parameter #icon_component data-skip "10"' \
		(PDParam (icon_component,"data-skip") = "10")
	entry 'Every 20th point' \
		'parameter #icon_component data-skip "20"' \
		(PDParam (icon_component,"data-skip") = "20")
	entry 'Every 30th point' \
		'parameter #icon_component data-skip "30"' \
		(PDParam (icon_component,"data-skip") = "30")
endmenu

