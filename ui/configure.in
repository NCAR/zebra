# Process this file with autoconf to produce a configure script.
AC_INIT(rdssui, [20110602], rdp-support@eol.ucar.edu)
AC_CONFIG_SRCDIR([ui.c])
AM_INIT_AUTOMAKE

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

#
# Checks for libraries.
#
AC_CHECK_LIB([m], [pow], ,
	[echo "Cannot find math library"; exit 1])
AC_SEARCH_LIBS([tgetstr], [ncurses termcap], ,
	[echo "Cannot find ncurses/termcap library"; exit 1])
# Link to libnsl and libsocket if we can find them (i.e., on Solaris systems),
# otherwise we probably get the necessary stuff from libc (i.e., under Linux)
AC_CHECK_LIB([nsl], [gethostbyname])
AC_CHECK_LIB([socket], [socket])

#
# X is required
#
AC_PATH_X

if test "$no_x" != "yes"; then
	DEFS="$DEFS -DXSUPPORT -DSMEMENU"
	LIBS="-lXt -lX11 $LIBS"
fi

AM_CONDITIONAL(WITH_X, [test x$no_x = x])

#
# Add to LDFLAGS and INCLUDES if X files are not in locations
# searched by default.
#
if test x$x_libraries != x; then 
	LDFLAGS="$LDFLAGS -L$x_libraries"
fi

if test x$x_includes != x; then 
	INCLUDES="$INCLUDES -I$x_includes"
fi

#
# HPUX may not have libXaw, libXext, and libXmu, so check explicitly and
# complain if one is missing
#
AC_CHECK_LIB([Xmu], [XmuMakeAtom], ,
	[echo "Cannot find X11 library libXmu"; exit 1])
AC_CHECK_LIB([Xext], [XextCreateExtension], ,
	[echo "Cannot find X11 extension library libXext"; exit 1])
AC_CHECK_LIB([Xaw], [XawInitializeWidgetSet], ,
	[echo "Cannot find Athena widget library libXaw"; exit 1])

#
# Expect to find RDSS libraries under our installation directory
#
LDFLAGS="$LDFLAGS -L${libdir}"

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T

# Checks for library functions.
AC_PROG_GCC_TRADITIONAL

AC_CONFIG_SUBDIRS([LiteClue])
AC_CONFIG_FILES([Makefile])
AC_OUTPUT
